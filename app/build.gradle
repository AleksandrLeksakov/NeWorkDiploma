apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'dagger.hilt.android.plugin'

def secretProperties = new Properties()
def secretPropertiesFile = rootProject.file('secret.properties')
if (secretPropertiesFile.exists()) {
    secretPropertiesFile.withReader('UTF-8') { reader ->
        secretProperties.load(reader)
    }
}

def apiKey = secretProperties.getProperty('API_KEY', '')
def mapkitApiKey = secretProperties.getProperty('MAPKIT_API_KEY', '')

android {
    namespace "ru.netology.nework"
    compileSdkVersion 36

    defaultConfig {
        applicationId "ru.netology.nework"
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 1
        versionName "1.0"

        // Используем API ключ из secret.properties
        buildConfigField "String", "API_KEY", "\"$apiKey\""

        // API ключ для Yandex MapKit
        buildConfigField "String", "MAPKIT_API_KEY", "\"$mapkitApiKey\""

        buildConfigField "String", "BASE_URL", '"http://94.228.125.136:8080/"'

        // Для Yandex MapKit в манифест
        manifestPlaceholders = [
                usesCleartextTraffic: true,
                yandexMapkitApiKey: mapkitApiKey
        ]
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
        freeCompilerArgs += ["-Xannotation-default-target=param-property"]
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders.usesCleartextTraffic = false
            buildConfigField "String", "BASE_URL", '"https://94.228.125.136:8080/"'
        }
        debug {
            manifestPlaceholders.usesCleartextTraffic = true
            buildConfigField "String", "BASE_URL", '"http://94.228.125.136:8080/"'
        }
    }
}

dependencies {
    def core_version = "1.17.0"
    def appcompat_version = "1.7.1"
    def constraintlayout_version = "2.2.1"
    def swiperefreshlayout_version = "1.2.0"
    def coordinatorlayout_version = "1.3.0"
    def recyclerview_version = "1.4.0"
    def activity_version = "1.12.2"
    def lifecycle_version = "2.10.0"
    def mdc_version = "1.13.0"
    def nav_version = "2.9.6"
    def room_version = "2.8.4"
    def firebase_version = "34.1.0"
    def glide_version = "5.0.5"
    def retrofit_version = "3.0.0"
    def retrofitgson_version = "3.0.0"
    def okhttplogging_version = "5.3.2"
    def coroutines_version = "1.10.2"
    def imagepicker_version = "2.1"
    def paging_version = "3.3.6"
    def play_services_base_version = "18.9.0"
    def core_desugaring_version = "2.1.5"
    def exoPlayerVersion = "1.2.1"
    def yandex_mapkit_version = "4.4.0-lite"
    def hilt_version = "2.57.2"

    implementation 'com.google.android.material:material:1.13.0'
    implementation 'commons-io:commons-io:2.21.0'
    implementation 'androidx.documentfile:documentfile:1.1.0'

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.core:core-ktx:$core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swiperefreshlayout_version"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$coordinatorlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "com.google.android.material:material:$mdc_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.android.gms:play-services-base:$play_services_base_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitgson_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttplogging_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    ksp "com.github.bumptech.glide:compiler:$glide_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:$coroutines_version"
    implementation "com.github.dhaval2404:imagepicker:$imagepicker_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$core_desugaring_version"
    implementation("androidx.media3:media3-exoplayer:$exoPlayerVersion")
    implementation("androidx.media3:media3-exoplayer-dash:$exoPlayerVersion")
    implementation("androidx.media3:media3-ui:$exoPlayerVersion")
    // Yandex MapKit
    implementation "com.yandex.android:maps.mobile:$yandex_mapkit_version"
}